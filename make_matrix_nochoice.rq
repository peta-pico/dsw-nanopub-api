prefix fip: <https://w3id.org/fair/fip/terms/>
prefix dct: <http://purl.org/dc/terms/>
prefix dce: <http://purl.org/dc/elements/1.1/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix np: <http://www.nanopub.org/nschema#>

select distinct ?fip_index ?fip_title ?community ?c ?supercommunity ?sc ?question ?q ?sort ?nochoice ?resource ?res ?reslabel ?rel ?resourcetype ?startdate ?enddate ?date where {
  ?np np:hasAssertion ?assertion .
  graph npa:graph { ?np dct:created ?date . }
  graph ?assertion {
    ?decl a fip:FIP-Declaration .
    # grlc gives an error for some reason if these two optional statements are at the end of the graph block:
    #optional { ?decl dcat:startDate ?startdate . }
    #optional { ?decl dcat:endDate ?enddate . }
    ?decl a ?nochoice ;
      fip:refers-to-question ?question ;
      fip:declared-by ?community .
  }
  optional {
     ?community dct:isPartOf ?supercommunity .
  }

  graph npa:graph { ?index_np npa:hasValidSignatureForPublicKey ?pubkey . }
  ?index_np np:hasAssertion ?index_a .
  ?index_np np:hasPublicationInfo ?index_i .
  graph ?index_a {
    ?fip_index npx:includesElement ?np .
  }
  graph ?index_i {
    ?fip_index dce:title ?fip_title_l .
  }
  filter not exists {
    ?fip_newer_index npx:includesElement ?newer_np ;
      dce:title ?fip_title_l .
    graph npa:graph { ?newer_np dct:created ?newer_date . }
    filter (?newer_date > ?date)
  }
  filter not exists {
    graph npa:graph {
      ?index_np_new npa:hasHeadGraph ?nh .
      ?index_np_new npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?nh {
      ?index_np_new np:hasPublicationInfo ?ni .
    }
    graph ?ni {
      ?index_np_new npx:supersedes ?index_np .
    }
  }
  filter not exists {
    graph npa:graph {
      ?index_np_r npa:hasHeadGraph ?rh .
      ?index_np_r npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?index_np_r np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?index_np .
    }
  }
  values ?nochoice { fip:FIP-No-Choice-Declaration fip:No-Choice-Declaration }
  bind (replace(str(?community), ".*#", "") as ?c)
  bind (replace(str(?supercommunity), ".*#", "") as ?sc)
  bind (replace(str(?question), "^.*-([^-MD]+(-[MD]+)?)$", "$1") as ?q)
  bind (concat(replace(?q, "F|M", "0"), "x") as ?sort)
  bind (str(?fip_title_l) as ?fip_title)
  bind ("" as ?resource)
  bind ("" as ?res)
  bind ("" as ?reslabel)
  bind ("" as ?rel)
  bind ("" as ?resourcetype)
  filter (?c != "ENVRI")
  filter (!strends(?fip_title, "2022$"))
} order by ?sort ?c
