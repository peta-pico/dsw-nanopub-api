#+ summary: List non-qualified FAIR Enabling/Supporting Resources
#+ method: GET
#+ pagination: 10000
#+ endpoint: https://query.np.trustyuri.net/repo/empty

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?np ?label (group_concat(distinct ?typelabel; separator = ", ") as ?types) ?date where {

values ?service {
  <https://query.np.trustyuri.net/repo/type/3ef5b11551ebc44e34f4bca614b387204447656ce195314532769ca7b20143fe>
  <https://query.np.trustyuri.net/repo/type/070fa9a0f87a189bc2eb9d4c1a14f68f11538cc2229b9f4fd6ac026a3d2ce570>
  <https://query.np.trustyuri.net/repo/type/6a09747868afc83837e38d1bac79362fbcc3e2032cff2f2d244f4e24cbd085c6>
  <https://query.np.trustyuri.net/repo/type/92efd7a0ea4be4e01ec0817ccec87f975203b30addcc3166a204498ffed73b66>
  <https://query.np.trustyuri.net/repo/type/2d7aa7a2eac5d58b5cf928ab5e5a71ed1bda00845942f26fc31647c42c7c58a3>
}
service ?service {
  graph npa:graph {
    ?np dct:created ?date .
    filter(str(?date) > "2022")
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
    ?np np:hasAssertion ?a .
    filter not exists { ?npx npx:invalidates ?np ; npa:hasValidSignatureForPublicKey ?pubkey . }
    filter not exists { ?np npx:hasNanopubType npx:ExampleNanopub . }
  }
  graph ?a {
    #?thing a <https://w3id.org/fair/fip/terms/FAIR-Supporting-Resource> .
    ?thing a ?type .
    ?thing rdfs:label ?labelx .
  }
}
  
filter( ?type != <https://w3id.org/fair/fip/terms/FAIR-Supporting-Resource> )
filter( ?type != <https://w3id.org/fair/fip/terms/Available-FAIR-Supporting-Resource> )
filter( ?type != <https://w3id.org/fair/fip/terms/FAIR-Supporting-Resource-to-be-Developed> )
filter( ?type != <https://w3id.org/fair/fip/terms/FAIR-Enabling-Resource> )
filter( ?type != <https://w3id.org/fair/fip/terms/Available-FAIR-Enabling-Resource> )
filter( ?type != <https://w3id.org/fair/fip/terms/FAIR-Enabling-Resource-to-be-Developed> )
bind(replace(str(?type), '^.*/([^/]+)$', '$1') as ?typelabel)
bind(str(?labelx) as ?label)
filter not exists { service <https://query.np.trustyuri.net/repo/type/39692876d0a7881c82e9a2cb6dc8859fb16fa69fb41cea2ace2f8fa70bc52aed> {
  graph npa:graph {
    ?disapproval npa:hasValidSignatureForPublicKey ?dpubkey .
    ?disapproval np:hasAssertion ?da .
    filter not exists { ?disapprovalx npx:invalidates ?disapproval ; npa:hasValidSignatureForPublicKey ?dpubkey . }
  }
  graph <https://w3id.org/np/RAkVk6aKWsmgcuKtPA7OSp2h-GbDfx8xrz_kJAAgY8Xa4#assertion> {
    ?dpubkeys npx:hasPublicKey ?dpubkey .
  }
  graph ?da {
    ?dsomebody npx:disapprovesOf ?np .
  }
} }
filter not exists { service <https://query.np.trustyuri.net/repo/type/cd51a3ac3e109fd12aacd5a324e14ebff644ce209c916e4250a3f50c2ddf0051> {
  graph npa:graph {
    ?qualification npa:hasValidSignatureForPublicKey ?qpubkey .
    ?qualification np:hasAssertion ?qa .
    filter not exists { ?qualificationx npx:invalidates ?qualification ; npa:hasValidSignatureForPublicKey ?qpubkey . }
  }
  graph <https://w3id.org/np/RAkVk6aKWsmgcuKtPA7OSp2h-GbDfx8xrz_kJAAgY8Xa4#assertion> {
    ?qpubkeys npx:hasPublicKey ?qpubkey .
  }
  graph ?qa {
    ?qsomebody npx:qualifies ?np .
  }
} }
}
group by ?np ?label ?date
order by desc(?date)
