prefix fip: <https://w3id.org/fair/fip/terms/>

select distinct ?community ?c ?question ?q ?sort ?resource ?res ?reslabel ?rel ?resourcetype where {
  ?decl a fip:FIP-Declaration ;
    fip:refers-to-question ?question ;
    fip:declared-by ?community ;
    ?rel ?resource .
    values ?rel { fip:declares-current-use-of fip:declares-planned-use-of }
    optional {?resource rdfs:label ?resourcelabel}
    optional {
      values ?resourcetype { fip:Available-FAIR-Enabling-Resource fip:FAIR-Enabling-Resource-to-be-Developed }
      ?resource a ?resourcetype
    }
  bind (replace(str(?community), ".*#", "") as ?c)
  bind (replace(str(?question), "^.*-([^-MD]+(-[MD]+)?)$", "$1") as ?q)
  bind (concat(replace(?q, "F|M", "0"), "x") as ?sort)
  bind (replace(str(?resource), "^.*?(#|/)([^/#]*/?[^/#]*)/?$", "$2") as ?res)
  bind (str(?resourcelabel) as ?reslabel)
  filter (?c != "ENVRI")
} order by ?sort ?c
